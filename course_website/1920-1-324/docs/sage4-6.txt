####
# An Introduction to Sage for Linear Algebra
# by Mckenzie West
# Last Updated: 11/7/2019
####
# https://sagecell.sagemath.org/

#####
# Section 4.6: Rank of a Matrix and Systems of Linear Equations
#####

s,t=var('s,t')

#Define a 3x3 matrix with nullity 2
A = matrix([[-2,-1,2],[3,3/2,-3],[3,3/2,-3]])

#Define a vector for which Ax=b has solutions
b = vector([3,-9/2,-9/2])
#Define a particular solution to Ax=b 
#(This was found by row reducing [A|b] and setting both of the parameters to 1)
u = vector([-1,1,1])

#Sketch the nullspace of A via the parametrization given by the basis of the null space
Nb = A.right_kernel().basis()
P = parametric_plot3d(s*Nb[0]+t*Nb[1],(s,-2,2),(t,-2,2))
#Include the vectors representing a basis for the nullspace
P += arrow((0,0,0),Nb[0], thickness=3, color="black")
P += arrow((0,0,0),Nb[1], thickness=3, color="black")
#Sketch the solution set to Ax=b by translating the parametrixation of the null space
P += parametric_plot3d(s*Nb[0]+t*Nb[1]+u, (s,-2,2), (t,-2,2), color="gold", opacity=0.5)
#Include the vector u, that is a particular solution to Ax=b
P += arrow((0,0,0), u, color="red")
P.show()